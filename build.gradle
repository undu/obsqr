buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:2.3.1'
		classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
	}
}

apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'

android {
	compileSdkVersion 25
	buildToolsVersion = "25.0.2"

	signingConfigs {
		release
	}

	defaultConfig {
		minSdkVersion 15
		targetSdkVersion 25
		versionCode 16
		versionName "3.0"
		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
	}

	buildTypes {
		release {
			minifyEnabled false
			signingConfig signingConfigs.release
		}
	}

	productFlavors {
		zbar {
			buildConfigField "boolean", "HAS_WIFI_PERMISSIONS", "false"
			applicationId 'trikita.obsqr'
		}
		gmsvision {
			buildConfigField "boolean", "HAS_WIFI_PERMISSIONS", "false"
			applicationId 'trikita.googleplay.obsqr'
		}
	}

	lintOptions {
		abortOnError false
	}
}

allprojects {
	repositories {
		jcenter()
	}
	project.ext {
		buildToolsVersion = "25.0.2"
		supportLibVersion = "25.3.1"
		runnerVersion = "0.5"
		rulesVersion = "0.5"
	}
}

dependencies {
	gmsvisionCompile 'com.google.android.gms:play-services-vision:10.2.1'
	compile 'com.android.support:appcompat-v7:' + project.ext.supportLibVersion;
	compile 'com.android.support:support-annotations:' + project.ext.supportLibVersion;
	// Solve dependency conflicts between app and test app
	androidTestCompile 'com.android.support:support-annotations:' + project.ext.supportLibVersion;
	androidTestCompile 'com.android.support.test:runner:' + project.ext.runnerVersion;
	androidTestCompile 'com.android.support.test:rules:' + project.ext.rulesVersion;
}

apply from: 'https://raw.github.com/trikita/gradle-android-signing/master/gradle-android-signing.gradle'
